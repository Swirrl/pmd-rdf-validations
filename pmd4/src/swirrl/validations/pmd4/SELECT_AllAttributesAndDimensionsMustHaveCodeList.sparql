PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#>

SELECT REDUCED ?comp_prop WHERE {
  VALUES ?comp_kind {
      qb:attribute
      qb:dimension
    }
  ?dsd a qb:DataStructureDefinition .
    # ok if dsd has component with qb:measure
    ?dsd qb:component [?comp_kind ?comp_prop] .

  FILTER NOT EXISTS {
    ?comp_prop qb:codeList ?cs .
  }
}
# This validation validates an extra PMD constraint that attributes
# and dimensions used in a dataset should contain at least one
# qb:codeList triple on the component property itself that identifies
# the code lists that hold metadata on the component values themselves.
#
# In particular we make use of this in PMD to join labels onto
# component values, when generating dataset downloads etc, where we
# need to know the component values ahead of time (to avoid having to
# join in queries that are repeated many times).
#
# Not including these links will result in downloads that contain
# URI's rather than labels.
