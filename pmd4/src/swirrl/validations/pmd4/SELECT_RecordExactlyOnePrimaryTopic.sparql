PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX ui:      <http://www.w3.org/ns/ui#>
PREFIX dcat:    <http://www.w3.org/ns/dcat#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>

SELECT ?record ?count {
    {
        SELECT ?record (COUNT(?pt) AS ?count) 
        WHERE {
            ?record a dcat:CatalogRecord .
            ?record foaf:primaryTopic ?pt .
        }
        GROUP BY ?record
        HAVING (?count > 1)
    }
    UNION {
        SELECT ?record ?count {
            ?record a dcat:CatalogRecord .
            FILTER NOT EXISTS {
                ?record foaf:primaryTopic ?pt .
            }
            BIND(0 AS ?count)
        }
    }
}
