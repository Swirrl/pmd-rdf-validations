PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qb:      <http://purl.org/linked-data/cube#>

SELECT ?dsd WHERE {
  ?dsd a qb:DataStructureDefinition .
  
  FILTER NOT EXISTS {
    # qb:componentProperty or qb:measure etc
    ?component_attachment rdfs:subPropertyOf* qb:componentProperty .
    ?dsd qb:component [?component_attachment ?measure] .
    ?measure a qb:MeasureProperty .
  }
}

#! IC-3. DSD includes measure - Every qb:DataStructureDefinition must include at least one declared measure. 

# Note - this includes a reasoning workaround by looking for subProperties of qb:componentProperty. 
######################################################################
####                ORIGINAL ASK QUERY FROM SPEC  IC-3            ####
# 
# ASK {
#  ?dsd a qb:DataStructureDefinition .
#  FILTER NOT EXISTS { ?dsd qb:component [qb:componentProperty [a qb:MeasureProperty]] }
# }
# 
#######################################################################